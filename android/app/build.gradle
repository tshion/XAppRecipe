plugins {
    id 'com.android.application'

    // Kotlin
    id 'kotlin-android'
    id 'kotlin-kapt'

    // AndroidX Navigation
    id "androidx.navigation.safeargs.kotlin"

    // Google OSS
    id "com.google.android.gms.oss-licenses-plugin"
}

android {
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    buildToolsVersion build_tools_version
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion compile_sdk_version
    defaultConfig {
        aaptOptions {
            // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
            // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
        applicationId "work.shion.xapprecipe"
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode appver_code
        versionName appver_name


        // 地図の基本URL
        buildConfigField "String", "URL_MAP",
            "\"https://www.google.co.jp/maps\""

        // Twitter のURL
        buildConfigField "String", "URL_Twitter",
            "\"https://twitter.com/shion_engineer\""

        // YouTube のURL
        buildConfigField "String", "URL_YouTube",
            "\"https://www.youtube.com/channel/UCAB_s7KJI0Ms6pjjSASte2w\""
    }
    flavorDimensions "mode"
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        exclude "DebugProbesKt.bin"
    }
    productFlavors {
        dev {
            dimension "mode"
            versionName "dev-${appver_name}"
        }

        prd {
            dimension "mode"
        }
    }
}

repositories {
    flatDir {
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation project(':capacitor-android')
    implementation project(':capacitor-cordova-android-plugins')

    // Java 8+
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${ver_desugar_jdk}"

    // Local Project
    implementation project(":app_core")
    implementation project(":app_data")

    // 共通設定
    testImplementation UnitTestLocal

    // AndroidPreparation
    debugImplementation "work.shion.androidpreparation:debugger:${ver_prep_debugger}"
    implementation "work.shion.androidpreparation:intentbuilder:${ver_prep_intent}"
    implementation "work.shion.androidpreparation:webviewbuilder:${ver_prep_web}"

    // AndroidX
    implementation "androidx.biometric:biometric:${ver_droidx_biometric}"
    implementation "androidx.browser:browser:${ver_droidx_browser}"
    implementation "androidx.camera:camera-camera2:${ver_droidx_camera}"
    implementation "androidx.camera:camera-lifecycle:${ver_droidx_camera}"
    implementation 'androidx.camera:camera-view:1.0.0-alpha32'
    implementation "androidx.constraintlayout:constraintlayout:${ver_droidx_constraint}"
    implementation "androidx.core:core-ktx:${ver_droidx_core}"
    implementation "androidx.fragment:fragment-ktx:${ver_droidx_fragment}"
    implementation "androidx.navigation:navigation-fragment-ktx:${ver_droidx_navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${ver_droidx_navigation}"
    implementation "androidx.recyclerview:recyclerview:${ver_droidx_recyclerview}"
    implementation "androidx.work:work-runtime-ktx:${ver_droidx_workmanager}"

    // Hyperion
    devImplementation "com.willowtreeapps.hyperion:hyperion-core:${ver_hyperion}"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-plugin:${ver_hyperion}"
    devImplementation "com.willowtreeapps.hyperion:hyperion-plugin:${ver_hyperion}"

    // Google
    implementation "com.google.android.gms:play-services-oss-licenses:${ver_oss_licenses}"
    kapt "com.google.auto.service:auto-service:${ver_auto_service}"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${ver_kotlin_coroutines}"

    // Material Design
    implementation "com.google.android.material:material:${ver_material}"

    // PermissionsDispatcher
    implementation "com.github.permissions-dispatcher:ktx:${ver_permissions_dispatcher_ktx}"

    // Picasso
    implementation "com.squareup.picasso:picasso:${ver_picasso}"

    // ZXing
    implementation("com.journeyapps:zxing-android-embedded:${ver_zxing_embedded}") { transitive = false }
    implementation "com.google.zxing:core:${ver_zxing}"
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch (Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
