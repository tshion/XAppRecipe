plugins {
    id 'com.android.library'

    // Kotlin 関連
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    buildToolsVersion build_tools_version
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion compile_sdk_version
    defaultConfig {
        consumerProguardFiles "consumer-rules.pro"
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"


        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        // XApp API V1 の基本パス(TODO: 重複管理の見直し)
        buildConfigField "String", "API_XAPP_V1",
            "\"${properties.getProperty('url_api_xapp_v1')}\""
    }
    kotlinOptions {
        freeCompilerArgs += '-Xexplicit-api=warning'
        jvmTarget = '1.8'
    }
    namespace 'com.github.tshion.xapprecipe_data'
    packagingOptions {
        resources {
            excludes += ['DebugProbesKt.bin']
        }
    }
}

dependencies {
    // Java 8+
    coreLibraryDesugaring libs.desugarJdk

    // Local Project
    implementation project(":app_core")

    // 共通設定
    androidTestImplementation libPack.unitTestLocal
    testImplementation libPack.unitTestLocal

    // AndroidX
    implementation libs.jetpack.core
    androidTestImplementation libs.jetpack.test.espresso
    androidTestImplementation libs.jetpack.test.rules

    // Kotlin Coroutines
    implementation libs.kotlinx.coroutines

    // Moshi
    implementation libPack.moshi
    kapt libs.moshi.kotlinCodegen

    // OkHttp
    implementation platform("com.squareup.okhttp3:okhttp-bom:${libver.okhttp}")
    testImplementation "com.squareup.okhttp3:logging-interceptor"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver"
    testImplementation "com.squareup.okhttp3:mockwebserver"
    implementation "com.squareup.okhttp3:okhttp"

    // Retrofit
    implementation libPack.retrofit

    // Robolectric
    testImplementation libs.robolectric
}
